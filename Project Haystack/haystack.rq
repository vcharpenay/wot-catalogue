prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix ph: <https://project-haystack.org/def/ph/3.9.7#> 
prefix phIct: <https://project-haystack.org/def/phIct/3.9.7#> 
prefix phScience: <https://project-haystack.org/def/phScience/3.9.7#> 
prefix phIoT: <https://project-haystack.org/def/phIoT/3.9.7#> 

# curl https://project-haystack.dev/download/defs.ttl > haystack-owl.ttl
# sparql --data haystack-owl.ttl --query haystack.rq > haystack.ttl

construct {
  ?marker a skos:Concept ;
          rdfs:label ?label ;
          skos:definition ?def ;
          rdfs:seeAlso ?source .
  ?parent skos:narrower ?marker .
  ?other skos:related ?marker . 

  ?marker skos:inScheme ph: .
} where {
  values ?top { phIoT:site phIoT:equip phIoT:space phIoT:point phScience:phenomenon phScience:quantity }
  ?marker rdfs:subClassOf+ ?top ;
          rdfs:label ?label ;
          rdfs:comment ?def ;
          rdfs:subClassOf ?parent .
  optional {
    ?marker phScience:quantityOf ?other
  }
  optional {
    ?marker ph:wikipedia ?url .
    bind (iri(str(?url)) as ?source)
  }
}