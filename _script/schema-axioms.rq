PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <http://schema.org/>
prefix wikibase: <http://wikiba.se/ontology#>
prefix bd: <http://www.bigdata.com/rdf#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix wdt: <http://www.wikidata.org/prop/direct/>
construct {
    ?from a rdfs:Class ; rdfs:label ?lfrom ; rdfs:comment ?deffrom .
    ?to a rdfs:Class ; rdfs:label ?lto ; rdfs:comment ?defto .
    ?from_r a rdf:Property ; schema:domainIncludes ?from ; schema:rangeIncludes ?to ; rdfs:label ?lr ; rdfs:comment ?defr ; rdfs:subPropertyOf ?r .
} where {
    graph <tag:tmp> {
        ?from ?r ?to filter (strstarts(str(?r), str(wdt:)) && ?r != wdt:P279 && ?r != wdt:P31)
        bind (iri(replace(str(?r), "prop/direct", "entity")) as ?ri) # indirect property
    }
    graph <tag:wikidata> {
        optional { ?from schema:name ?lfrom filter (lang(?lfrom) = "en") }
        optional { ?from schema:description ?deffrom filter (lang(?deffrom) = "en") }
        optional { ?to schema:name ?lto filter (lang(?lto) = "en") }
        optional { ?to schema:description ?defto filter (lang(?defto) = "en") }
        optional { ?ri schema:name ?lr filter (lang(?lr) = "en") }
        optional { ?ri schema:description ?defr filter (lang(?defr) = "en") }
    }
    bind (iri(concat(str(?from), "_", replace(str(?r), str(wdt:), ""))) as ?from_r)
}