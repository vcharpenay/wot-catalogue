prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ontolex: <http://www.w3.org/ns/lemon/ontolex#>
prefix schema: <http://schema.org/>

# sparql --data BACnet/bacnet.ttl --data BLE\ GATT/gatt.ttl --data LWM2M/ipso.ttl --data OCF/oneiota.ttl --data oneM2M/haim.ttl --data Project\ Haystack/haystack.ttl --data wikidata-mapping.nt --data wikidata-hierarchy-reviewed-matches.nt --data wikidata-hierarchy-labels.en.nt --query _script/list-overlapping-matches.rq

select ?e ?label ?count where {
    {
        select distinct ?e where {
            ?c1 ontolex:isEvokedBy ?tag1 .
            ?tag1 skos:closeMatch ?e .

            ?c2 ontolex:isEvokedBy ?tag2 .
            ?tag2 skos:closeMatch ?e .

            filter (?c1 != ?c2)
            filter exists {
                ?c1 skos:inScheme ?scheme1 .
                ?c2 skos:inScheme ?scheme2 .
                filter (?scheme1 != ?scheme2)
            }
        }
    } optional {
        select distinct ?e ?label where {
            ?e schema:name ?l filter (lang(?l) = "en")
            bind (str(?l) as ?label)
        }
    }

    {
        select ?e (count(*) as ?count) where {
            {
                select distinct ?e ?scheme where {
                    ?c ontolex:isEvokedBy ?tag ; skos:inScheme ?scheme .
                    ?tag skos:closeMatch ?e .
                }
            }
        } group by ?e
    }
} order by desc(?count)