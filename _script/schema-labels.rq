PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <http://schema.org/>
prefix wikibase: <http://wikiba.se/ontology#>
prefix bd: <http://www.bigdata.com/rdf#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix wdt: <http://www.wikidata.org/prop/direct/>
construct {
    ?entity rdfs:label ?l ; rdfs:comment ?def .
} where {
    {
        select distinct ?entity ?l ?def where {
            graph <tag:tmp> { ?from ?r ?to }
            bind (?from as ?entity)
            graph <tag:wikidata> {
                optional { ?entity schema:name ?l filter (lang(?l) = "en") }
                optional { ?entity schema:description ?def filter (lang(?def) = "en") }
            }
        }
    }
    union
    {
        select distinct ?entity ?l ?def where {
            graph <tag:tmp> { ?from ?r ?to }
            bind (?to as ?entity)
            graph <tag:wikidata> {
                optional { ?entity schema:name ?l filter (lang(?l) = "en") }
                optional { ?entity schema:description ?def filter (lang(?def) = "en") }
            }
        }
    }
    union
    {
        select distinct ?entity ?l ?def where {
            graph <tag:tmp> { ?from ?r ?to } filter (?r != wdt:P31 && ?r != wdt:P279)
            bind (iri(replace(str(?r), str(wdt:), str(wd:))) as ?ri) # indirect property
            bind (iri(concat(str(?from), "_", replace(str(?ri), str(wd:), ""))) as ?entity)
            graph <tag:wikidata> {
                optional { ?ri schema:name ?l filter (lang(?l) = "en") }
                optional { ?ri schema:description ?def filter (lang(?def) = "en") }
            }
        }
    }
}