prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix wd: <http://www.wikidata.org/entity/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix : <http://ti.rw.fau.de/vocab#>

# sparql --data wikidata-closure.nt --data wikidata-neighbors-labels.en.nt --data wikidata-hierarchy-labels.en.nt --query _script/generate-closure-dot.rq --results "CSV" > wikidata-closure.dot

select (concat("graph { ", ?edges, " }") as ?graph) where {
    {
        select (group_concat(?edge; separator = ";") as ?edges) where {
            {
                select distinct ?e1 ?e2 where {
                    ?e1 owl:topObjectProperty ?e2 .
                }
            }

            optional { ?e1 schema:name ?l1 filter (lang(?l1) = "en") }
            bind (if(bound(?l1), ?l1, replace(str(?e1), str(wd:), "")) as ?from)
            optional { ?e2 schema:name ?l2 filter (lang(?l2) = "en") }
            bind (if(bound(?l2), ?l2, replace(str(?e2), str(wd:), "")) as ?to)
            bind (concat("<", ?from, "> -- <", ?to, ">") as ?edge)
        }
    }
}