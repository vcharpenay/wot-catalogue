prefix schema: <http://schema.org/>
prefix wd: <http://www.wikidata.org/entity/>
prefix wdt: <http://www.wikidata.org/prop/direct/>

# sparql --data wikidata-closure.nt --data wikidata-labels.en.nt --query _script/generate-property-hierarchy-dot.rq --results "CSV" > property-hierarchy.dot

select (concat("digraph { ", ?edges, " }") as ?digraph) where {
    {
        select (group_concat(?edge; separator = ";\n") as ?edges) where {
            {
                select ?child (count(*) as ?count) where {
                    ?descendant (wdt:P31|wdt:P279)* ?child .
                    filter exists {?descendant (wdt:P31|wdt:P279)* wd:Q937228 } # property
                } group by ?child order by desc(?count)
            }
            filter (?count > 25)

            ?child wdt:P31|wdt:P279 ?parent
            filter not exists {
                ?child wdt:P31|wdt:P279 ?other .
                ?other (wdt:P31|wdt:P279)+ ?parent .
            }

            optional { ?child schema:name ?lc filter (lang(?lc) = "en") }
            bind (if(bound(?lc), ?lc, replace(str(?child), str(wd:), "")) as ?c)
            optional { ?parent schema:name ?lp filter (lang(?lp) = "en") }
            bind (if(bound(?lp), ?lp, replace(str(?parent), str(wd:), "")) as ?p)
            bind (concat("<", ?c, "> -> <", ?p, ">") as ?edge)
        }
    }
}