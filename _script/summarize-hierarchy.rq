prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix wd: <http://www.wikidata.org/entity/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix : <http://ti.rw.fau.de/vocab#>

# sparql --data wikidata-hierarchy.nt --data wikidata-hierarchy-labels.en.nt --query _script/summarize-hierarchy.rq --results "N-Triples" > wikidata-summary.nt

construct {
    ?e1 rdfs:subClassOf ?e2 .
    ?e1 rdfs:label ?label1 . ?e2 rdfs:label ?label2 .
    # distribution of weights is used to adapt the value of ?min (~ inflection point in the power low distribution)
    ?e1 :weight ?count1 .
    ?e2 :weight ?count2 .
} where {
    bind (100 as ?min)
    # duplicated subquery to compare entities
    {
        select ?e1 (count(?child) as ?count1) where {
            ?tag skos:closeMatch ?child .
            ?child (wdt:P31|wdt:P279)* ?e .
            bind (?e as ?e1)
        } group by ?e1 order by desc(?count1)
    }
    {
        select ?e2 (count(?child) as ?count2) where {
            ?tag skos:closeMatch ?child .
            ?child (wdt:P31|wdt:P279)* ?e .
            bind (?e as ?e2)
        } group by ?e2 order by desc(?count2)
    }
    filter (?e1 != ?e2 && ?count1 >= ?min && ?count2 >= ?min)

    optional { ?e1 schema:name ?label1 . filter (lang(?label1) = "en") }
    optional { ?e2 schema:name ?label2 . filter (lang(?label2) = "en") }
    
    ?e1 (wdt:P31|wdt:P279)+ ?e2
    filter not exists {
        ?e1 (wdt:P31|wdt:P279)+ ?other .
        ?other (wdt:P31|wdt:P279)+ ?e2 . 
    }
}
