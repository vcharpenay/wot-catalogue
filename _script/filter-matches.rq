PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix schema: <http://schema.org/> 
prefix wd: <http://www.wikidata.org/entity/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix ontolex: <http://www.w3.org/ns/lemon/ontolex#>

# sparql --data wikidata-filtered-entities.nt --data wikidata-mapping.ttl --query _script/filter-matches.rq --results "N-Triples" > wikidata-matches.nt

construct { ?tag skos:closeMatch ?e }
where {
    ?tag ontolex:evokes ?e .

    # whitelist and blacklist filtering of parent entities
    values ?whitelist { wd:Q1183543 wd:Q937228 }
    ?e (wdt:P31|wdt:P279)* ?whitelist .

    # filtering of entities subsumed by other matches
    filter not exists {
        ?tag ontolex:evokes ?other .
        ?e (wdt:P31|wdt:P279)+ ?other .
    }
}