PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix schema: <http://schema.org/> 
prefix wd: <http://www.wikidata.org/entity/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix ontolex: <http://www.w3.org/ns/lemon/ontolex#>
prefix : <https://www.vcharpenay.link/wot-catalogue/#>

# cp wikidata-matches.nt wikidata-matches-$i.nt
# sparql --data wikidata-entities.nt --data wikidata-blacklist.ttl --data wikidata-whitelist.ttl --data wikidata-matches.nt --query _script/filter-matches.rq --results "N-Triples" > wikidata-matches.nt

construct { ?tag skos:closeMatch ?e }
where {
  ?tag skos:closeMatch ?e .

  filter not exists {
    ?ep a :BlacklistItem .
    ?e wdt:P31?/wdt:P279* ?ep .
  }

  filter not exists {
    ?tag skos:closeMatch ?ep .
    filter (?e != ?ep)

    ?es a :WhitelistItem .
    ?ep wdt:P31?/wdt:P279* ?es .
  }

 # filtering of entities subsumed by other matches
  filter not exists {
    ?tag skos:closeMatch ?ep .
    ?e (wdt:P31/wdt:P279*)|wdt:P279+ ?ep .
  }
}
