PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <http://schema.org/>
prefix wikibase: <http://wikiba.se/ontology#>
prefix bd: <http://www.bigdata.com/rdf#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix wdt: <http://www.wikidata.org/prop/direct/>
construct {
    ?child a rdfs:Class .
    ?parent a rdfs:Class .
    ?child rdfs:subClassOf ?parent .
} where {
    {
        select distinct ?e where {
    		graph <tag:tmp> { { ?e ?r ?other } union { ?other ?r ?e } }
        }
    }
    # TODO look up the whole Wikidata hierarchy (not only closure)?
    graph <tag:tmp> {
        ?e (wdt:P31|wdt:P279)? ?child . # depth 2
        ?child wdt:P31|wdt:P279 ?parent .
        filter not exists {
            ?child wdt:P31|wdt:P279 ?other .
            ?other (wdt:P31|wdt:P279)+ ?parent
        }
    }
}