PREFIX ex: <http://example.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX httpList: <http://plugins.linkedpipes.com/ontology/e-httpGetFiles#> 

CONSTRUCT { 
  <http://localhost/resource/configuration> a httpList:Configuration ;
    httpList:reference ?ref .
  ?ref a httpList:Reference;
    httpList:fileUri ?uri ;
    httpList:fileName ?filename .
} WHERE {
  ?s a ?type;
    skos:altLabel ?a;
    OPTIONAL{ ?s ex:test ?t .}
  FILTER(?type IN(skos:Concept, skos:ConceptScheme, skos:Collection))
  BIND(IF(BOUND(?t),?t, ?a) AS ?name)
  BIND(REPLACE(str(?name),' ','%20') AS ?nameurl)
  BIND(URI(CONCAT("http://localhost/resource/ref/",?nameurl)) AS ?ref)
  BIND(CONCAT("https://www.wikidata.org/w/api.php?action=wbsearchentities&search=",?nameurl,"&language=en&format=json&strictlanguage=true&limit=3") AS ?uri)
  BIND(CONCAT("wikidataSearch",?nameurl,".json") as ?filename)
}
