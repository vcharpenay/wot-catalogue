PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ex: <http://example.org/>
INSERT {
  ?s ?npred ?a .
} WHERE {
  ?s a ?type;
     skos:altLabel ?alt .
  FILTER(?type IN (skos:Concept, skos:ConceptScheme, skos:Collection))
  FILTER(CONTAINS(?alt, " "))
  BIND(" " AS ?separator)
    BIND(STRLEN(REPLACE(REPLACE(str(?alt), "([^ ]+)", "Å") , "[^Å]", "")) as ?count)
  VALUES ?n {0 1 2 3 4 5 6 7 8 9 10 11 12}
  FILTER(?n < ?count)
  BIND(CONCAT("^(?:[^",?separator,"]*",?separator,"){",str(?n),"}([^",?separator,"]*).*$|.*") as ?regex)
  BIND(REPLACE(?alt, ?regex, "$1") AS ?component)
  FILTER(STRLEN(?component) > 0)
  BIND(?component as ?a)
  BIND(URI(CONCAT(STR(ex:),"1gram",STR(?n))) AS ?npred)
}
